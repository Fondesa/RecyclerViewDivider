/*
 * Copyright (c) 2020 Giorgio Antonioli
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    apply from: "repositories.gradle", to: buildscript
    apply from: "parse-dependencies.gradle"

    dependencies {
        classpath deps.kotlinPlugin
        classpath deps.ktlint
    }
}

apply from: "repositories.gradle"
apply plugin: "groovy"
apply plugin: "kotlin"
apply from: "ktlint.gradle"

// We can't apply kotlin.gradle because otherwise the warnings will be treated as errors.
// Since Kotlin 1.4.x, we can't compile buildSrc because the Gradle Wrapper contains an old Kotlin version internally.
tasks.configureEach {
    // Matches the compile*Kotlin tasks regardless of the applied plugins.
    it.name.eachMatch("^compile.*Kotlin\$") {
        kotlinOptions {
            jvmTarget = "1.8"
            allWarningsAsErrors = false
            freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
        }
    }
}

dependencies {
    implementation deps.androidPlugin
    implementation deps.bintrayPlugin
    implementation deps.dokkaPlugin
    implementation deps.gitHubReleasePlugin
    implementation deps.kotlinStdLib
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == "com.squareup.okhttp3" && details.requested.name == "okhttp") {
            details.useVersion "4.0.0"
            details.because "Fixes \"com.github.breadmoirai:github-release\" since it doesn't depend on OkHttp 4."
        }
    }
}
